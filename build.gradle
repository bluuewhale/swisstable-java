plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.2'
    id "com.vanniktech.maven.publish" version "0.28.0"
}

group = 'io.github.bluuewhale'
version = '0.1.7'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    jmhImplementation 'org.eclipse.collections:eclipse-collections:11.1.0'
    jmhImplementation 'it.unimi.dsi:fastutil:8.5.13'
}

testing.suites {
    test {
        useJUnitJupiter('5.10.0')

        dependencies {
            implementation 'it.unimi.dsi:fastutil:8.5.13'
            implementation 'org.openjdk.jol:jol-core:0.17'
            implementation 'org.eclipse.collections:eclipse-collections:11.1.0'
        }
        targets.all {
            testTask.configure {
                jvmArgs '--add-modules', 'jdk.incubator.vector'
            }
        }
    }
    apacheTest(JvmTestSuite) {
        useJUnitJupiter('5.10.0')

        dependencies {
            implementation(project())
            implementation("org.apache.commons:commons-lang3:3.20.0")
            implementation("org.apache.commons:commons-collections4:4.5.0")
            implementation("org.apache.commons:commons-collections4:4.5.0") {
                artifact { classifier = "tests" }
            }
        }
        targets.all {
            testTask.configure {
                jvmArgs '--add-modules', 'jdk.incubator.vector'
            }
        }
    }
    googleTest(JvmTestSuite) {
        useJUnit()

        dependencies {
            implementation(project())
            implementation 'com.google.guava:guava-testlib:33.5.0-jre'
        }
        targets.all {
            testTask.configure {
                jvmArgs '--add-modules', 'jdk.incubator.vector'
            }
        }
    }
    tasks.check.configure {
        dependsOn(tasks.withType(Test))
    }
}

// JIT / ASM (HotSpot PrintAssembly) runner for SwissMap.findIndexHashed.
// Usage:
//   ./gradlew jitAsm | tee jit-asm.txt
// NOTE: PrintAssembly requires an hsdis library for your JDK. See docs/JIT-ASM.md.
tasks.register('jitAsm', JavaExec) {
    group = 'verification'
    description = 'Run a small harness that heats up SwissMap.findIndexHashed and prints the generated machine code via -XX:+PrintAssembly (requires hsdis).'

    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'io.github.bluuewhale.hashsmith.SwissMapAsmProbe'

    jvmArgs = [
            '-Xms256m',
            '-Xmx256m',
            '-Xbatch',
            // Force C2 (server compiler) so the assembly reflects final hot-path optimizations.
            '-XX:-TieredCompilation',
            '-XX:CICompilerCount=2',
            '-XX:CompileThreshold=1000',
            // Assembly
            '-XX:+UnlockDiagnosticVMOptions',
            '-XX:+PrintAssembly',
            // Reduce noise: try to compile/print only the method we care about.
            '-XX:CompileCommand=quiet',
            '-XX:CompileCommand=compileonly,io.github.bluuewhale.hashsmith.SwissMap::findIndexHashed',
            '-XX:CompileCommand=print,io.github.bluuewhale.hashsmith.SwissMap::findIndexHashed',
    ]
}

// JIT / ASM (HotSpot PrintAssembly) runner for ConcurrentSwissMap.get (and the underlying SwissMap probe loop).
// Usage:
//   ./gradlew jitAsmConcurrent | tee jit-asm-concurrent.txt
// NOTE: PrintAssembly requires an hsdis library for your JDK.
tasks.register('jitAsmConcurrent', JavaExec) {
    group = 'verification'
    description = 'Run a small harness that heats up ConcurrentSwissMap.get and prints the generated machine code via -XX:+PrintAssembly (requires hsdis).'

    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'io.github.bluuewhale.hashsmith.ConcurrentSwissMapAsmProbe'

    jvmArgs = [
            '-Xms256m',
            '-Xmx256m',
            '-Xbatch',
            // Force C2 (server compiler) so the assembly reflects final hot-path optimizations.
            '-XX:-TieredCompilation',
            '-XX:CICompilerCount=2',
            '-XX:CompileThreshold=1000',
            // Assembly
            '-XX:+UnlockDiagnosticVMOptions',
            '-XX:+PrintAssembly',
            // Reduce noise: try to compile/print only the methods we care about.
            '-XX:CompileCommand=quiet',
            '-XX:CompileCommand=compileonly,io.github.bluuewhale.hashsmith.ConcurrentSwissMap::get',
            '-XX:CompileCommand=print,io.github.bluuewhale.hashsmith.ConcurrentSwissMap::get',
            // Also print the underlying SwissMap probe loop to correlate costs.
            '-XX:CompileCommand=compileonly,io.github.bluuewhale.hashsmith.SwissMap::findIndexHashed',
            '-XX:CompileCommand=print,io.github.bluuewhale.hashsmith.SwissMap::findIndexHashed',
    ]
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
}

javadoc {
    options.addStringOption('-add-modules', 'jdk.incubator.vector')
}

jmh {
    warmupIterations = 3
    iterations = 5
    fork = 1
    benchmarkMode = ['avgt']
    timeUnit = 'ns'
    jvmArgs = ['--add-modules', 'jdk.incubator.vector']
}

mavenPublishing {
    publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    coordinates("io.github.bluuewhale", "hashsmith", version)

    pom {
        name.set("HashSmith")
        description.set("High-performance open-addressing hash tables for the JVM.")
        url.set("https://github.com/bluuewhale/hash-smith")

        licenses {
            license {
                name.set("MIT License")
                url.set("https://opensource.org/licenses/MIT")
            }
        }
        developers {
            developer {
                id.set("bluuewhale")
                name.set("bluuewhale")
                url.set("https://github.com/bluuewhale")
            }
        }
        scm {
            connection.set("scm:git:https://github.com/bluuewhale/hash-smith.git")
            developerConnection.set("scm:git:ssh://git@github.com:bluuewhale/hash-smith.git")
            url.set("https://github.com/bluuewhale/hash-smith")
        }
    }
}

signing {
    def keyFile = System.getenv('GPG_SIGNING_KEY')
    def pass = System.getenv('GPG_SIGNING_PASSWORD')

    if (keyFile && pass) {
        def keyText = new File(keyFile).getText('UTF-8')
        useInMemoryPgpKeys(keyText, pass)
    } else {
        logger.warn("GPG_SIGNING_KEY / GPG_SIGNING_PASSWORD environment variables are not set")
    }
}

afterEvaluate {
    tasks.named("generateMetadataFileForMavenPublication") {
        dependsOn(tasks.named("plainJavadocJar"))
    }
}
