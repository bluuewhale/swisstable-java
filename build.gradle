plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.2'
    id "com.vanniktech.maven.publish" version "0.28.0"
}

group = 'io.github.bluuewhale'
version = '0.1.7'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.eclipse.collections:eclipse-collections:11.1.0'
    testImplementation 'it.unimi.dsi:fastutil:8.5.13'
    testImplementation 'org.openjdk.jol:jol-core:0.17'

    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    jmhImplementation 'org.eclipse.collections:eclipse-collections:11.1.0'
    jmhImplementation 'it.unimi.dsi:fastutil:8.5.13'
    jmhImplementation 'io.github.fzhinkin:xctraceprof:0.0.3'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

test {
    useJUnitPlatform()
    jvmArgs '--add-modules', 'jdk.incubator.vector'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--add-modules', 'jdk.incubator.vector']
}

javadoc {
    options.addStringOption('-add-modules', 'jdk.incubator.vector')
}

jmh {
    warmupIterations = 3
    iterations = 5
    fork = 1
    benchmarkMode = ['avgt']
    timeUnit = 'ns'
    jvmArgs = [ '--add-modules=jdk.incubator.vector', '--enable-preview' ]
    jvmArgsAppend = [ '--add-modules=jdk.incubator.vector', '--enable-preview' ]
}

tasks.named("jmhRunBytecodeGenerator") {
    jvmArgs = ["--add-modules=jdk.incubator.vector", "--enable-preview"]
}


mavenPublishing {
    publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    coordinates("io.github.bluuewhale", "hashsmith", version)

    pom {
        name.set("HashSmith")
        description.set("High-performance open-addressing hash tables for the JVM.")
        url.set("https://github.com/bluuewhale/hash-smith")

        licenses {
            license {
                name.set("MIT License")
                url.set("https://opensource.org/licenses/MIT")
            }
        }
        developers {
            developer {
                id.set("bluuewhale")
                name.set("bluuewhale")
                url.set("https://github.com/bluuewhale")
            }
        }
        scm {
            connection.set("scm:git:https://github.com/bluuewhale/hash-smith.git")
            developerConnection.set("scm:git:ssh://git@github.com:bluuewhale/hash-smith.git")
            url.set("https://github.com/bluuewhale/hash-smith")
        }
    }
}

signing {
  def keyFile = System.getenv('GPG_SIGNING_KEY')
  def pass = System.getenv('GPG_SIGNING_PASSWORD')

  if (keyFile && pass) {
      def keyText = new File(keyFile).getText('UTF-8')
      useInMemoryPgpKeys(keyText, pass)
  } else {
      logger.warn("GPG_SIGNING_KEY / GPG_SIGNING_PASSWORD environment variables are not set")
  }
}

afterEvaluate {
    tasks.named("generateMetadataFileForMavenPublication") {
        dependsOn(tasks.named("plainJavadocJar"))
    }
}
